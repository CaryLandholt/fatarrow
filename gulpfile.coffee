{APP_NAME, BOWER_COMPONENTS, SCRIPTS, STYLES} = require './config.coffee'

BOWER_DIRECTORY       = '.components/'
CHANGELOG_FILE        = 'CHANGELOG.md'
COMPONENTS_DIRECTORY  = "#{BOWER_DIRECTORY}_/"
DIST_DIRECTORY        = 'dist/'
DOCS_DIRECTORY        = 'docs/'
E2E_DIRECTORY         = 'e2e/'
FONTS_DIRECTORY       = 'fonts/'
PORT                  = 8181
SCRIPTS_MIN_DIRECTORY = 'scripts/'
SCRIPTS_MIN_FILE      = 'scripts.min.js'
SRC_DIRECTORY         = 'src/'
STYLES_MIN_DIRECTORY  = 'styles/'
STYLES_MIN_FILE       = 'styles.min.css'
TEMP_DIRECTORY        = '.temp/'
VENDOR_DIRECTORY      = 'vendor/'
VIEWS                 =  ['**/*.html', '!index.html']

bower                 = require 'bower'
childProcess          = require 'child_process'
clean                 = require 'gulp-clean'
coffee                = require 'gulp-coffee'
coffeeLint            = require 'gulp-coffeelint'
concat                = require 'gulp-concat'
connect               = require 'gulp-connect'
conventionalChangelog = require 'conventional-changelog'
es                    = require 'event-stream'
filter                = require 'gulp-filter'
flatten               = require 'gulp-flatten'
fs                    = require 'fs'
gulp                  = require 'gulp'
gutil                 = require 'gulp-util'
jade                  = require 'gulp-jade'
karma                 = require 'karma'
less                  = require 'gulp-less'
markdown              = require 'gulp-markdown'
minifyCss             = require 'gulp-minify-css'
minifyHtml            = require 'gulp-minify-html'
ngClassify            = require 'gulp-ng-classify'
open                  = require 'gulp-open'
path                  = require 'path'
pkg                   = require './package.json'
protractor            = require 'gulp-protractor'
q                     = require 'q'
rev                   = require 'gulp-rev'
template              = require 'gulp-template'
templateCache         = require 'gulp-angular-templatecache'
typeScript            = require 'gulp-typescript'
uglify                = require 'gulp-uglify'

appUrl = "http://localhost:#{PORT}"

bowerJson =
	_comment: 'THIS FILE IS AUTOMATICALLY GENERATED.  DO NOT EDIT.'
	name: pkg.name
	version: pkg.version
	dependencies: {}

bowerComponents = do ->
	components = {}

	for component, value of BOWER_COMPONENTS
		for version, componentTypes of value
			bowerJson.dependencies[component] = version

			for componentType, files of componentTypes
				isArray = Array.isArray files
				filesToAdd = if isArray then files else [files]

				filesToAdd = filesToAdd.map (file) ->
					path.join component, file

				key = path.join component, componentType

				if not components[key]
					components[key] = []

				components[key] = components[key].concat filesToAdd

	components

fs.writeFile 'bower.json', JSON.stringify bowerJson, {}, '\t'

env         = gutil.env
isProd      = env.prod? or env.production?
isVerbose   = env.verb? or env.verbose?
isWindows   = /^win/.test(process.platform)
log         = gutil.log
logger      = if isVerbose then (title, args...) -> log gutil.colors.yellow(title), gutil.colors.green('âœ“'), gutil.colors.white(args) else ->
unixifyPath = (p) -> p.replace /\\/g, '/'

onError = (e) ->
	isArray  = Array.isArray e
	err      = e.message or e
	errors   = if isArray then err else [err]
	messages = (error for error in errors)

	log gutil.colors.red message for message in messages
	@emit 'end'

processCoffeeScript = ->
	deferred     = q.defer()
	sources      = ['**/*.coffee'].concat if isProd then ['!**/*.spec.coffee', '!**/*.backend.coffee'] else []
	moduleLogger = (args...) -> logger 'CoffeeScript', args

	options =
		coffeeScript:
			sourceMap: true
		coffeeLint:
			arrow_spacing:
				level: 'error'
			indentation:
				value: 1
			max_line_length:
				level: 'ignore'
			no_tabs:
				level: 'ignore'
		ngClassify:
			appName: APP_NAME or 'app'
			data:
				isProd: isProd

	gulp
		.src sources, cwd: TEMP_DIRECTORY

		# coffeeLint
		.pipe coffeeLint options.coffeeLint
		.on 'error', onError
		.on 'end', -> moduleLogger 'coffeeLint'

		# coffeeLint.reporter
		.pipe coffeeLint.reporter()
		.on 'error', onError
		.on 'end', -> moduleLogger 'coffeeLint.reporter'

		# ngClassify
		.pipe ngClassify options.ngClassify
		.on 'error', onError
		.on 'end', -> moduleLogger 'ngClassify'

		# coffee
		.pipe coffee options.coffeeScript
		.on 'error', onError
		.on 'end', -> moduleLogger 'coffee'

		# destination
		.pipe gulp.dest TEMP_DIRECTORY
		.on 'end', -> moduleLogger()

		# end
		.on 'end', -> deferred.resolve()

	deferred.promise

processCss = ->
	deferred              = q.defer()
	sources               = STYLES
	optimizedStylesSource = if isProd then sources else []
	optimizedStylesFilter = filter optimizedStylesSource
	destinationDirectory  = if isProd then path.join(DIST_DIRECTORY, STYLES_MIN_DIRECTORY) else DIST_DIRECTORY
	moduleLogger          = (args...) -> logger 'CSS', args

	options =
		keepSpecialComments: 0

	gulp
		.src sources, cwd: TEMP_DIRECTORY

		.pipe optimizedStylesFilter

		# concat
		.pipe concat STYLES_MIN_FILE
		.on 'error', onError
		.on 'end', -> moduleLogger 'concat' if isProd

		# minifyCss
		.pipe minifyCss options
		.on 'error', onError
		.on 'end', -> moduleLogger 'minifyCss' if isProd

		# rev
		.pipe rev()
		.on 'error', onError
		.on 'end', -> moduleLogger 'rev'

		.pipe optimizedStylesFilter.restore()

		# destination
		.pipe gulp.dest destinationDirectory
		.on 'end', -> moduleLogger()

		# end
		.on 'end', -> deferred.resolve()

	deferred.promise

processJade = ->
	deferred     = q.defer()
	sources      = ['**/*.jade']
	moduleLogger = (args...) -> logger 'Jade', args

	options =
		pretty: true

	gulp
		.src sources, cwd: TEMP_DIRECTORY

		# jade
		.pipe jade options
		.on 'error', onError
		.on 'end', -> moduleLogger 'jade'

		# destination
		.pipe gulp.dest TEMP_DIRECTORY
		.on 'end', -> moduleLogger()

		# end
		.on 'end', -> deferred.resolve()

	deferred.promise

processJavaScript = ->
	deferred               = q.defer()
	sources                = SCRIPTS.concat if isProd then ['!**/*.spec.js', '!**/*.backend.js', '!**/angular-mocks.js'] else []
	optimizedScriptsSource = if isProd then sources else []
	optimizedScriptsFilter = filter optimizedScriptsSource
	destinationDirectory   = if isProd then path.join(DIST_DIRECTORY, SCRIPTS_MIN_DIRECTORY) else DIST_DIRECTORY
	moduleLogger           = (args...) -> logger 'JavaScript', args

	gulp
		.src sources, cwd: TEMP_DIRECTORY

		.pipe optimizedScriptsFilter

		# concat
		.pipe concat SCRIPTS_MIN_FILE
		.on 'error', onError
		.on 'end', -> moduleLogger 'concat' if isProd

		# uglify
		.pipe uglify()
		.on 'error', onError
		.on 'end', -> moduleLogger 'uglify' if isProd

		.pipe rev()
		.on 'error', onError
		.on 'end', -> moduleLogger 'rev'

		.pipe optimizedScriptsFilter.restore()

		# destination
		.pipe gulp.dest destinationDirectory
		.on 'end', -> moduleLogger()

		# end
		.on 'end', -> deferred.resolve()

	deferred.promise

processLess = ->
	deferred     = q.defer()
	sources      = ['**/*.less']
	moduleLogger = (args...) -> logger 'Less', args

	options =
		sourceMap: true
		sourceMapBasepath: path.resolve TEMP_DIRECTORY

	gulp
		.src sources, cwd: TEMP_DIRECTORY

		# less
		.pipe less options
		.on 'error', onError
		.on 'end', -> moduleLogger 'less'

		# destination
		.pipe gulp.dest TEMP_DIRECTORY
		.on 'end', -> moduleLogger()

		# end
		.on 'end', -> deferred.resolve()

	deferred.promise

processMarkdown = ->
	deferred     = q.defer()
	sources      = ['**/*.{md,markdown}']
	moduleLogger = (args...) -> logger 'Markdown', args

	gulp
		.src sources, cwd: TEMP_DIRECTORY

		# markdown
		.pipe markdown()
		.on 'error', onError
		.on 'end', -> moduleLogger 'markdown'

		# destination
		.pipe gulp.dest TEMP_DIRECTORY
		.on 'end', -> moduleLogger()

		# end
		.on 'end', -> deferred.resolve()

	deferred.promise

processSourceScripts = ->
	return true if isProd

	deferred     = q.defer()
	sources      = ['**/*.{coffee,ts,js.map}']
	moduleLogger = (args...) -> logger 'source scripts', args

	gulp
		.src sources, cwd: TEMP_DIRECTORY

		# desination
		.pipe gulp.dest DIST_DIRECTORY
		.on 'end', -> moduleLogger()

		# end
		.on 'end', -> deferred.resolve()

	deferred.promise

processSourceStyles = ->
	return true if isProd

	deferred     = q.defer()
	sources      = ['**/*.less']
	moduleLogger = (args...) -> logger 'source styles', args

	gulp
		.src sources, cwd: TEMP_DIRECTORY

		# destination
		.pipe gulp.dest DIST_DIRECTORY
		.on 'end', -> moduleLogger()

		# end
		.on 'end', -> deferred.resolve()

	deferred.promise

processSourceViews = ->
	return true if isProd

	deferred     = q.defer()
	sources      = ['**/*.{jade,md,markdown}']
	moduleLogger = (args...) -> logger 'source views', args

	gulp
		.src sources, cwd: TEMP_DIRECTORY

		# destination
		.pipe gulp.dest DIST_DIRECTORY
		.on 'end', -> moduleLogger()

		# end
		.on 'end', -> deferred.resolve()

	deferred.promise

processTypeScript = ->
	deferred     = q.defer()
	sources      = ['**/*.ts'].concat if isProd then ['!**/*.spec.ts', '!**/*.backend.ts'] else []
	moduleLogger = (args...) -> logger 'TypeScript', args

	gulp
		.src sources, cwd: TEMP_DIRECTORY

		# typeScript
		.pipe typeScript()
		.on 'error', onError
		.on 'end', -> moduleLogger 'typeScript'

		# destination
		.pipe gulp.dest TEMP_DIRECTORY
		.on 'end', -> moduleLogger()

		# end
		.on 'end', -> deferred.resolve()

	deferred.promise

processViews = ->
	return true if isProd

	deferred     = q.defer()
	sources      = VIEWS
	moduleLogger = (args...) -> logger 'views', args

	gulp
		.src sources, cwd: TEMP_DIRECTORY

		# destination
		.pipe gulp.dest DIST_DIRECTORY
		.on 'end', -> moduleLogger()

		# end
		.on 'end', -> deferred.resolve()

	deferred.promise

gulp.task 'bower', ->
	deferred     = q.defer()
	moduleLogger = (args...) -> logger 'Bower', args

	options =
		directory: BOWER_DIRECTORY

	components = []

	for component, value of BOWER_COMPONENTS
		for version, files of value
			components.push "#{component}##{version}"

	bower
		# bower
		.commands.install components, {}, options
		.on 'error', onError
		.on 'end', -> moduleLogger()

		# end
		.on 'end', (results) -> deferred.resolve results

	deferred.promise

gulp.task 'build', ['scripts', 'styles', 'views', 'fonts', 'spa']

gulp.task 'changelog', ->
	options =
		repository: pkg.repository.url
		version: pkg.version
		file: CHANGELOG_FILE
		log: gutil.log

	conventionalChangelog options, (err, log) ->
		fs.writeFile CHANGELOG_FILE, log

gulp.task 'clean', ['clean:working'], ->
	sources      = [BOWER_DIRECTORY]
	moduleLogger = (args...) -> logger 'clean', args

	gulp
		.src sources

		# clean
		.pipe clean()
		.on 'error', onError
		.on 'end', -> moduleLogger()

gulp.task 'clean:working', ->
	sources      = [COMPONENTS_DIRECTORY, TEMP_DIRECTORY, DIST_DIRECTORY, DOCS_DIRECTORY]
	moduleLogger = (args...) -> logger 'clean:working', args

	gulp
		.src sources

		# clean
		.pipe clean()
		.on 'error', onError
		.on 'end', -> moduleLogger()

gulp.task 'copy:temp', ['clean:working', 'normalizeComponents'], ->
	sources      = ["#{SRC_DIRECTORY}**", "#{COMPONENTS_DIRECTORY}**"]
	moduleLogger = (args...) -> logger 'clean:working', args

	gulp
		.src sources

		# destination
		.pipe gulp.dest TEMP_DIRECTORY
		.on 'end', -> moduleLogger()

gulp.task 'default', if isProd then ['open'] else ['open', 'watch', 'build', 'test']

gulp.task 'e2e', ->
	e2eConfigFile       = path.join './', TEMP_DIRECTORY, 'e2e-config.coffee'
	phantomjsBinaryPath = if isWindows then './node_modules/.bin/phantomjs.cmd' else './node_modules/phantomjs/bin/phantomjs'
	sources             = ['**/*.spec.{coffee,js}']

	# create temporary e2e-config file to avoid an additional config file
	# currently gulp-protractor requires one the existence of an e2e-config file
	do (e2eConfigFile) ->
		doesExist = fs.existsSync TEMP_DIRECTORY

		if !doesExist
			throw new Error 'The app must be currently running (gulp).'

		contents = 'exports.config = {}'

		fs.writeFileSync e2eConfigFile, contents

	options =
		configFile: e2eConfigFile
		args: [
			'--baseUrl', appUrl
			'--browser', 'phantomjs'
			'--capabilities.phantomjs.binary.path', phantomjsBinaryPath
		]

	gulp
		.src sources, cwd: E2E_DIRECTORY

		# protractor
		.pipe protractor.protractor options
		.on 'error', onError
		.on 'end', -> moduleLogger()

gulp.task 'e2e-driver', protractor.webdriver_standalone

gulp.task 'e2e-driver-update', protractor.webdriver_update

gulp.task 'fonts', ['copy:temp'], ->
	sources      = ['**/*.{eot,svg,ttf,woff}']
	src          = gulp.src sources, cwd: TEMP_DIRECTORY
	moduleLogger = (args...) -> logger 'fonts', args

	return if isProd
		src
			# flatten
			.pipe flatten()
			.on 'error', onError
			.on 'end', -> moduleLogger 'flatten'

			# destination
			.pipe gulp.dest path.join DIST_DIRECTORY, FONTS_DIRECTORY
			.on 'end', -> moduleLogger()

	src
		# destination
		.pipe gulp.dest DIST_DIRECTORY
		.on 'end', -> moduleLogger()

gulp.task 'karma', ->
	options =
		autoWatch: false
		background: true
		basePath: DIST_DIRECTORY
		browsers: [
			'PhantomJS'
		]
		colors: true
		files: SCRIPTS
		frameworks: [
			'jasmine'
		]
		keepalive: false
		logLevel: 'WARN'
		reporters: [
			'spec'
		]
		singleRun: true
		transports: [
			# 'websocket' # removed due to excessive socket connections
			'flashsocket'
			'xhr-polling'
			'jsonp-polling'
		]

	karma.server.start options

gulp.task 'minify:views', ['copy:temp'],->
	return true if not isProd

	sources      = VIEWS
	moduleLogger = (args...) -> logger 'minify:views', args

	options =
		empty: true
		quotes: true

	gulp
		.src sources, cwd: TEMP_DIRECTORY

		# minifyHtml
		.pipe minifyHtml options
		.on 'error', onError
		.on 'end', -> moduleLogger 'minifyHtml'

		# destination
		.pipe gulp.dest path.join TEMP_DIRECTORY
		.on 'end', -> moduleLogger()

gulp.task 'normalizeComponents', ['bower', 'clean:working'], ->
	promises     = []
	moduleLogger = (args...) -> logger 'normalizeComponents', args

	for componentType, files of bowerComponents
		promise = do (files, componentType) ->
			deferred = q.defer()
			sources = files

			gulp
				.src sources, cwd: BOWER_DIRECTORY

				# destination
				.pipe gulp.dest path.join COMPONENTS_DIRECTORY, VENDOR_DIRECTORY, componentType

				# end
				.on 'end', -> deferred.resolve()

			deferred.promise

		promises.push promise

	q
		.all promises
		.then -> moduleLogger()

gulp.task 'open', ['serve'], ->
	sources      = ['index.html']
	moduleLogger = (args...) -> logger 'open', args

	options =
		url: appUrl

	gulp
		.src sources, cwd: DIST_DIRECTORY

		# open
		.pipe open '', options
		.on 'error', onError
		.on 'end', -> moduleLogger()

gulp.task 'reload', ['build'], ->
	sources      = ['index.html']
	moduleLogger = (args...) -> logger 'reload', args

	gulp
		.src sources, cwd: DIST_DIRECTORY

		# reload
		.pipe connect.reload()
		.on 'error', onError
		.on 'end', -> moduleLogger()

gulp.task 'scripts', ['copy:temp', 'templateCache'], ->
	moduleLogger = (args...) -> logger 'scripts', args

	processCoffeeScript()
		.then processTypeScript
		.then processJavaScript
		.then processSourceScripts
		.then -> moduleLogger()

gulp.task 'serve', ['build'], ->
	connect.server
		livereload: !isProd
		port: PORT
		root: DIST_DIRECTORY

gulp.task 'spa', ['scripts', 'styles', 'views', 'fonts'], ->
	includify = ->
		scripts = []
		styles  = []

		bufferContents = (file) ->
			return if file.isNull()

			ext = path.extname file.path
			p   = unixifyPath(path.join('/', path.relative(file.cwd, file.path)))

			return if ext is '.js'
				scripts.push p

			return if ext is '.css'
				styles.push p

		endStream = ->
			payload = {scripts, styles}
			@emit 'data', payload
			@emit 'end', payload

		es.through bufferContents, endStream

	getIncludes = ->
		deferred     = q.defer()
		moduleLogger = (args...) -> logger 'includify', args

		sources = []
			.concat SCRIPTS
			.concat ['!**/*.spec.js']
			.concat STYLES

		gulp
			.src sources, cwd: DIST_DIRECTORY

			# includify
			.pipe includify()
			.on 'error', onError
			.on 'end', -> moduleLogger()

			# end
			.on 'end', (data) -> deferred.resolve data

		deferred.promise

	processSpa = (files) ->
		deferred     = q.defer()
		sources      = ['index.html']
		moduleLogger = (args...) -> logger 'SPA', args

		options =
			empty: true
			quotes: true

		data =
			appName: APP_NAME
			scripts: files.scripts
			styles: files.styles

		optimizedSource = if isProd then sources else []
		optimizedFilter = filter optimizedSource

		gulp
			.src sources, cwd: TEMP_DIRECTORY

			# template
			.pipe template data
			.on 'error', onError
			.on 'end', -> moduleLogger 'template'

			.pipe optimizedFilter

			# minifyHtml
			.pipe minifyHtml options
			.on 'error', onError
			.on 'end', -> moduleLogger 'minifyHtml' if isProd

			.pipe optimizedFilter.restore()

			# destination
			.pipe gulp.dest DIST_DIRECTORY
			.on 'end', -> moduleLogger()

			# end
			.on 'end', -> deferred.resolve()

		deferred.promise

	getIncludes()
		.then processSpa

gulp.task 'styles', ['copy:temp'], ->
	moduleLogger = (args...) -> logger 'styles', args

	processLess()
		.then processCss
		.then processSourceStyles
		.then -> moduleLogger()

gulp.task 'templateCache', ['copy:temp'], ->
	return true if not isProd

	sources      = VIEWS
	moduleLogger = (args...) -> logger 'templateCache', args

	options =
		module: APP_NAME
		root: '/'

	gulp
		.src sources, cwd: TEMP_DIRECTORY

		# templateCache
		.pipe templateCache options
		.on 'error', onError
		.on 'end', -> moduleLogger 'templateCache'

		# destination
		.pipe gulp.dest path.join TEMP_DIRECTORY, SCRIPTS_MIN_DIRECTORY
		.on 'end', -> moduleLogger()

gulp.task 'test', ['build'], ->
	return true if isProd

	# don't allow karma to block gulp
	command = if isWindows then '.\\node_modules\\.bin\\gulp.cmd' else 'gulp'
	spawn   = childProcess.spawn command, ['karma'], {stdio: 'inherit'}

gulp.task 'views', ['copy:temp'], ->
	moduleLogger = (args...) -> logger 'views', args

	processJade()
		.then processMarkdown
		.then processViews
		.then processSourceViews
		.then -> moduleLogger()

gulp.task 'watch', ['build'], ->
	return true if isProd

	gulp
		.watch "#{SRC_DIRECTORY}**/*.{coffee,css,html,jade,less,markdown,md,ts}", ['test', 'reload']